static def getBuildTypes(moduleProject) {
    def buildTypes = []
    moduleProject.android.buildTypes.each { type ->
        buildTypes.add(type.name)
    }
    return buildTypes.join(",")
}

def getModuleBuildDir(moduleName) {
    return project.findProject(moduleName).buildDir.toString()
}

def getModuleDir(moduleName) {
    return "${project.findProject(moduleName).projectDir.toString()}/src/main/java"
}

subprojects {
    def portumModuleName = "navigation"
    def androidPlugins = [
            "com.android.build.gradle.AppPlugin",
            "com.android.build.gradle.LibraryPlugin"
    ]
    def buildDir = getModuleBuildDir(portumModuleName)

    project.plugins.whenPluginAdded { plugin ->
        if (plugin.class.name in androidPlugins) {
            def buildTypes = getBuildTypes(project)
            project.android.defaultConfig {
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments += [
                                "portumPath": buildDir,
                                "buildTypes": buildTypes
                        ]
                    }
                }
            }
        }
    }
}