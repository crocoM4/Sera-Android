static def getBuildTypes(moduleProject) {
    def buildTypes = []
    moduleProject.android.buildTypes.each { type ->
        buildTypes.add(type.name)
    }
    return buildTypes.join(",")
}

def getModuleBuildDir(moduleName) {
    return project.findProject(moduleName).buildDir.toString()
}

subprojects {
    def aggregatorModuleName = "navigation"
    def androidPlugins = [
            "com.android.build.gradle.AppPlugin",
            "com.android.build.gradle.LibraryPlugin"
    ]
    def buildDir = getModuleBuildDir(aggregatorModuleName)

    project.plugins.whenPluginAdded { plugin ->
        if (plugin.class.name in androidPlugins) {
            def buildTypes = getBuildTypes(project)
            project.android.defaultConfig {
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments += [
                                "portumPath": buildDir,
                                "buildTypes": buildTypes
                        ]
                    }
                }
            }
        }
    }
}