import com.guerra.enrico.sera.buildsrc.Libs
import com.guerra.enrico.sera.buildsrc.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def configPropertiesFile = rootProject.file("config.properties")
def configProperties = new Properties()
configProperties.load(new FileInputStream(configPropertiesFile))

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        applicationId "com.guerra.enrico.sera"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.guerra.enrico.sera.SeraTestRunner"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    lintChecks project(":lint")

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(":base")
    implementation project(":base_android")
    implementation project(":models")
    implementation project(":domain")
    implementation project(":workers")
    implementation project(":features:splash")
    implementation project(":features:todos")
    implementation project(":features:goals")
    implementation project(":features:results")
    implementation project(":features:settings")

    implementation project(":common:resources")

    implementation project(":navigation")

    implementation Libs.Google.flexbox
    implementation Libs.Google.material
    implementation Libs.AndroidX.coreKtx
    implementation Libs.AndroidX.appCompat
    implementation Libs.AndroidX.constraintLayout
    implementation Libs.AndroidX.fragmentKtx
    implementation Libs.AndroidX.activityKtx

    // Coroutine
    implementation Libs.Kotlin.coroutines

    // ViewModel
    implementation Libs.Lifecycle.viewModelKtx

    // Hilt
    implementation Libs.Hilt.android
    kapt Libs.Hilt.androidCompiler
    implementation Libs.Hilt.lifecycleViewModel
    kapt Libs.Hilt.compiler
    implementation Libs.Hilt.work

    // Room
    implementation Libs.Room.runtime
    kapt Libs.Room.compiler
    implementation Libs.Room.ktx

    // Gson
    implementation Libs.Gson

    // Work Manager
    implementation Libs.WorkManager

    // Timber
    implementation Libs.Timber

    // Navigation
    implementation Libs.Navigation.fragmentKtx
    implementation Libs.Navigation.uiKtx

    // Unit Test
    testImplementation Libs.Junit
    testImplementation Libs.Robolectric
    testImplementation Libs.MockitoCore
    testImplementation Libs.Mockk.lib

    testImplementation Libs.AndroidX.Test.core
    testImplementation Libs.AndroidX.Test.archCore

    testImplementation Libs.Kotlin.Test.coroutines
    testImplementation Libs.Kotlin.Test.kotlin

    kaptTest Libs.Room.compiler

    // Instrument Test
    androidTestImplementation Libs.Junit

    androidTestImplementation Libs.AndroidX.Test.coreKtx
    androidTestImplementation Libs.AndroidX.Test.junitKtx
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.archCore

    androidTestImplementation Libs.Espresso.core
    androidTestImplementation Libs.Espresso.contrib
    androidTestImplementation Libs.Espresso.idlingResource
    androidTestImplementation Libs.Espresso.intents

    androidTestImplementation Libs.Mockk.android

    androidTestImplementation Libs.Hilt.Test.android
    kaptAndroidTest Libs.Hilt.Test.androidCompiler

    androidTestImplementation Libs.Hilt.lifecycleViewModel
    kaptAndroidTest Libs.Hilt.compiler
    androidTestImplementation Libs.Hilt.work

}
